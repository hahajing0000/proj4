if (isNeedHomeMoudle.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

def appInfo=rootProject.ext.android
def libs=rootProject.ext.dependencies

android {
    compileSdkVersion appInfo.compileSdkVersion
    buildToolsVersion appInfo.buildToolsVersion

    defaultConfig {
        minSdkVersion appInfo.minSdkVersion
        targetSdkVersion appInfo.targetSdkVersion
        versionCode appInfo.versionCode
        versionName appInfo.versionName


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        //arouter集成
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //配置资源前缀
    resourcePrefix "home_"

    sourceSets {
        main {
            if (isNeedHomeMoudle.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.appcompat
    implementation libs.constraintlayout
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation project(path: ':base_common')

    annotationProcessor libs.aroutercompiler
    implementation project(path: ':base_core')
}
